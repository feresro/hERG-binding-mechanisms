[[model]]
author: Myokit CellML 1 API
name: Lei_2019_37C_IKr_Model13
desc: "Lei et al. 2019 physiological model with modified Li et al. 2017 binding model."
# Initial values
ikr.O = 0
ikr.I = 0
ikr.CI = 0
ikr.OD = 0
ikr.ID = 0
ikr.CD = 0
ikr.C = 1
#ikr.D = 0

[ikr]
use membrane.V

IKr = p9 * O * (V - nernst.EK)
    in [nA]
    oxmeta: membrane_rapid_delayed_rectifier_potassium_current

k1 = p1 * exp(p2 * V)
k2 = p3 * exp(-p4 * V)
k3 = p5 * exp(p6 * V)
k4 = p7 * exp(-p8 * V)

p1 =  0.0765e-3 [mS/uF]
    in [mS/uF]
p2 =  9.05e-2 [1/mV]
    in [1/mV]
p3 =  2.84e-5 [mS/uF]
    in [mS/uF]
p4 =  4.74e-2 [1/mV]
    in [1/mV]
p5 =  1.03e-1 [mS/uF]
    in [mS/uF]
p6 =  2.13e-2 [1/mV]
    in [1/mV]
p7 =  0.801e-2 [mS/uF]
    in [mS/uF]
p8 =  2.96e-2 [1/mV]
    in [1/mV]
p9 = 31 [nS]
    in [nS]
    oxmeta: membrane_rapid_delayed_rectifier_potassium_current_conductance

dot(O) = ku * OD - emax * O + k1 * C + k4 * I - (k2 + k3) * O
dot(I) = psi * ID - emax * I + k1 * CI + k3 * O - (k2 + k4) * I
dot(CI) = k2 * I + k3 * C - (k1 + k4) * CI
dot(OD) = emax * O + kt * X * CD - (ku + kt) * OD
dot(CD) = kt * OD + kt * ID - (kt * X + kt * X) * CD
dot(ID) = emax * I + kt * X * CD - (psi + kt) * ID
dot(C) = - k3 * C + k4 * CI - k1 * C + k2 * O
#C = 1 - O - I - CI - OD - ID - CD

#dot(D) = 0
D = 0
    in [nM]

emax = kforward * D ^ hill
X = 1 / (1 + exp(-(V + vhalf) / 6.789))
psi = ku * k3 / k4

kforward = 1e-03 * 5 / 0.15 # ku * kmax / EC50^n
ku = 1e-03
hill = 1
vhalf = 8
kt = 3.5e-05 #fixed


[engine]
time = 0 bind time
    in [ms]
pace = 0 bind pace

[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]

[extracellular]
ko = 5.4 [mM]
    in [mM]
    oxmeta: extracellular_potassium_concentration
    label K_o

[intracellular]
ki = 130 [mM]
    in [mM]
    oxmeta: intracellular_potassium_concentration
    label K_i

[physical_constants]
F = 96485 [C/mol]
    in [C/mol]
R = 8314 [mJ/mol/K]
    in [mJ/mol/K]
T = 310 [K]
    in [K]

[nernst]
EK = physical_constants.R * physical_constants.T / physical_constants.F * log(extracellular.ko / intracellular.ki)
    in [mV]
    oxmeta: potassium_reversal_potential
